\documentclass[a4paper, english, 11pt]{article}

\usepackage[english]{babel}
\usepackage[utf8]{inputenc}

\usepackage[T1]{fontenc}
\usepackage[sc]{mathpazo}
\linespread{1.05}

\usepackage{courier}
\usepackage{listings}
\lstset {
	language=[Objective]Caml,
	breaklines=true,
	breakatwhitespace=true,
	showspaces=false,
	showstringspaces=false
	showtabs=false,
	tabsize=2,
	frame=l,
	basicstyle=\footnotesize\ttfamily
}

\usepackage{hyperref}

\usepackage[svgnames]{xcolor}

\usepackage{microtype}

\newcommand{\email}[1]{\href{mailto:#1}{\nolinkurl{#1}}}
\newcommand{\warningbox}[1]{ {\centering\fcolorbox{SaddleBrown}{NavajoWhite}{\begin{minipage}[t]{0.97 \linewidth}{ \textbf{WARNING}: #1 }\end{minipage}}} }

\begin{document}

\title { MySQL Protocol Tutorial }
\author { St√©phane Legrand <\email{stephleg@free.fr}> }
\maketitle

\begin{abstract}
This tutorial illustrates the use of the MySQL Protocol library, a native OCaml implementation of the MySQL client protocol.

You can download this library from \href[]{https://forge.ocamlcore.org/projects/ocmysqlprotocol/}{the MySQL Protocol home page}.
\end{abstract}

\newpage

\tableofcontents

\newpage

\section{Introduction}

The library has to be installed before using the code below. Please read the INSTALL file in the library archive. The OCaml source code of this tutorial is available in the \textit{examples} directory.

\section{Modules}

First, some convenient alias for the modules used.

\begin{lstlisting}
module Mp_client = Mysql_protocol.Mp_client;;
module Mp_data = Mysql_protocol.Mp_data;;
module Mp_execute = Mysql_protocol.Mp_execute;;
module Mp_result_set_packet = Mysql_protocol.Mp_result_set_packet;;
\end{lstlisting}

\section{Helper functions}

We define one function to print the result of SQL statements like INSERT, UPDATE, GRANT...

\begin{lstlisting}
let print_result sql r = 
  print_endline ("Result of the SQL statement \"" ^ sql ^ "\": \n  " ^ (Mp_client.dml_dcl_result_to_string r) ^ "\n")
;;
\end{lstlisting}

And two others to print the result of SELECT SQL statements.

\begin{lstlisting}
let print_row fields row = 
  let print_data f = 
    let (field_name, field_pos) = f in
    let data = List.nth row field_pos in
    print_endline ("  " ^ field_name ^ ": " ^ (Mp_data.to_string data))
  in
  let () = List.iter print_data fields in
  print_endline "  -- --  "
;;
let print_set sql r = 
  let (fields, rows) = r.Mp_result_set_packet.rows in
  let () = print_endline ("Result set for the SQL statement \"" ^ sql ^ "\": \n") in
  let print_rows =   
    let () = List.iter (print_row fields) rows in
    print_newline ()
  in
  print_rows
;;
\end{lstlisting}

\section{Configuration}

To be able to connect to the MySQL server, we have first to configure the client.

\begin{lstlisting}
let addr = Unix.inet_addr_loopback;;
let port = 3306;;
let sockaddr = Unix.ADDR_INET(addr, port);;

let db_user = "user_ocaml_ocmp";;
let db_password = "ocmp";;
let db_name = "test_ocaml_ocmp_utf8";;
\end{lstlisting}

The MySQL server is listening on the loopback interface and it uses the standard MySQL server port. The login to authenticate to the server is "user\_ocaml\_ocmp" with the password "ocmp". And we will use the "test\_ocaml\_ocmp\_utf8" database.

Now, we can create a configuration.

\begin{lstlisting}
let config = Mp_client.configuration  ~user:db_user ~password:db_password ~sockaddr:sockaddr ~databasename:db_name () in
\end{lstlisting}

Here, the default charset and collation is used. The default value for charset is \texttt{Utf8} and the default value for collation is \texttt{Utf8\_general\_ci}. If you want to specify an other value, you can use the \texttt{charset} parameter. For instance:
\begin{lstlisting}
   let config = Mp_client.configuration  ~user:db_user ~password:db_password ~sockaddr:sockaddr ~databasename:db_name ~charset:(Mp_charset.Latin1, Mp_charset.Latin1_swedish_ci) () in
\end{lstlisting}

To have the complete list of available charset and collation, you can read the documentation of the \texttt{Mp\_Charset} module.

\section{Connection}

Now, we can connect our client to the MySQL server.

\begin{lstlisting}
let connection = Mp_client.connect ~configuration:config () in
\end{lstlisting}

By default, the connection is not initialized right after the call to the \texttt{connect()} function. It's delayed until necessary (ie until the first real request). You can immediately force the connection by using the \texttt{force} parameter:

\begin{lstlisting}
let connection = Mp_client.connect ~configuration:config ~force:true () in
\end{lstlisting}

\section{Select database}

To specify the current database, use the following function:

\begin{lstlisting}
let () = Mp_client.use_database ~connection:connection ~databasename:db_name in
\end{lstlisting}

\section{Non prepared statement}

A non prepared statement is the simplest way to send a statement to the server. If your statement doesn't have any parameter (ie is a static string) and is used only a few times, it's usually sufficient.

\warningbox{ You SHOULD NOT use a non prepared statement if it contains a parameter with non trusted value. }

\subsection{Create}

The first step is to create the statement from the SQL string.

\begin{lstlisting}
let sql = "INSERT INTO ocmp_table (col1, col2) VALUES ('col1', 123.45)" in
let stmt = Mp_client.create_statement_from_string sql in
\end{lstlisting}

\subsection{Execute}

Next, we send the statement to the server to execute it.

\begin{lstlisting}
let r = Mp_client.execute ~connection:connection ~statement:stmt () in
\end{lstlisting}

\subsection{Get result}

After being executed, the statement result can be retrieved.

\subsubsection{Result without record}

For statement which returns only a simple result without any record (INSERT, UPDATE, DELETE, GRANT... statement), you can use the \texttt{get\_result\_ok()} function.

\begin{lstlisting}
let r = Mp_client.get_result_ok r in
\end{lstlisting}

To print this result, use the \texttt{print\_result()} helper function.

\begin{lstlisting}
let () = print_result sql r in
\end{lstlisting}

\subsubsection{Result with records}

For statement which returns records (typically SELECT statement), you can use the \texttt{get\_result\_set()} function.

\begin{lstlisting}
let r = Mp_client.get_result_set r in
\end{lstlisting}

To print this result, use the \texttt{print\_set()} helper function.

\begin{lstlisting}
let () = print_set sql r in 
\end{lstlisting}

\section{Prepared statement}

Especially when the statement includes some parameters, you should use a prepared statement. The parameters values will then be correctly enclosed in the statement by the MySQL server and all special characters will be automatically escaped. You can of course also use a prepared statement even if the statement doesn't have any parameter.

\subsection{Create}

The first step is the same as for a non prepared statement, you have to create the statement from the SQL string with the same function.

\begin{lstlisting}
let sql = "SELECT * FROM ocmp_table WHERE col1=?" in
let stmt = Mp_client.create_statement_from_string sql in
\end{lstlisting}

\subsection{Prepare}

Then, you prepare the statement.

\begin{lstlisting}
let prep = Mp_client.prepare ~connection:connection ~statement:stmt in
\end{lstlisting}

Once a statement has been prepared, you can execute it several times without calling the \texttt{prepare()} function again. 

\subsection{Execute}

You execute a prepared statement with the same function as for a non prepared one. Nonetheless, for a prepared statement, the \texttt{execute()} function may accept more parameters.

\subsubsection{Simple execute}

For the simplest use case (no parameters in the statement, no cursor), you execute the statement as for a non prepared one.

\begin{lstlisting}
let r = Mp_client.execute ~connection:connection ~statement:prep () in
\end{lstlisting}

\subsubsection{With parameters}

If you have some parameters in the statement, you first need to create the list of these parameters in the same order of appearance as in the statement. Please see the documentation for the \texttt{Mp\_data} module to have a complete list of data constructor and learn which one to use for each MySQL column types.

\begin{lstlisting}
let params = [Mp_data.Varstring "col2"; Mp_data.Decimal (Num.num_of_string "98765/100")] in
\end{lstlisting}

And you add the \texttt{params} function parameter for the execution.

\begin{lstlisting}
let r = Mp_client.execute ~connection:connection ~statement:prep ~params:params () in
\end{lstlisting}

\subsubsection{With cursor}

By default, no cursor is used when a prepared statement is executed. So the server will always return all the corresponding records. If you want to be able to fetch the result by parts (record by record for instance), you need to specify the cursor option in the \texttt{execute()} function.

\begin{lstlisting}
let stmt = Mp_client.execute ~connection:connection ~statement:prep ~params:params ~flag:Mp_execute.Cursor_type_read_only () in
\end{lstlisting}

\warningbox{ For now, only \texttt{Cursor\_type\_read\_only} type is supported. }

\subsection{Get result}

After being executed, the statement result can be retrieved.

\subsubsection{Result without record}

For statement which returns only a simple result without any record (INSERT, UPDATE, DELETE, GRANT... statement), there is no difference compared with the non prepared statements. You can also use the \texttt{get\_result\_ok()} function.

\begin{lstlisting}
let r = Mp_client.get_result_ok r in
\end{lstlisting}

To print this result, use the \texttt{print\_result()} helper function.

\begin{lstlisting}
let () = print_result sql r in
\end{lstlisting}

\subsubsection{Result with records}

For statement which returns records (typically SELECT statement), if you haven't used a cursor, you cannot use fetch. So you will retrieve all the rows with the same method as a non prepared statement.

\begin{lstlisting}
let r = Mp_client.get_result_set r in
\end{lstlisting}

To print this result, use the \texttt{print\_set()} helper function.

\begin{lstlisting}
let () = print_set sql r in 
\end{lstlisting}

If you have used a cursor, you have to use fetch to retrieve the records. By default, the \texttt{fetch()} function get one record at each call. To specify an other number, use the \texttt{nb\_rows} function parameter.

\begin{lstlisting}
let stmt = Mp_client.execute ~connection:connection ~statement:prep ~params:params ~flag:Mp_execute.Cursor_type_read_only () in
let () = 
  try
    while true do
      let rows = Mp_client.fetch ~connection:connection ~statement:stmt () in
      let rows = Mp_client.get_fetch_result_set rows in
      print_set sql rows
    done
  with
  | Mp_client.Fetch_no_more_rows -> () (* no more rows in the result *)
in
\end{lstlisting}

\subsection{Close statement}

When a prepared statement has become useless (ie you don't need to execute it again), you can and should destroy it.

\begin{lstlisting}
let () = Mp_client.close_statement ~connection:connection ~statement:prep in
\end{lstlisting}

\section{Ping server}

To avoid a timeout or to test the connection, you can send a ping to the server. No result is returned but an \texttt{Mp\_client.Error} exception can be raised.

\begin{lstlisting}
let () = Mp_client.ping ~connection:connection in
\end{lstlisting}

\section{Reset the session}

This is useful if you need to destroy the session context (temporary tables, session variables, etc.) in the MySQL server.

\begin{lstlisting}
let () = Mp_client.reset_session ~connection:connection in
\end{lstlisting}

\section{Catching errors}

Whenever the MySQL server returns an error, an \texttt{Mp\_client.Error} exception is raised.

\begin{lstlisting}
let stmt = Mp_client.create_statement_from_string ("BAD SQL QUERY") in
let () =
  try
    let stmt = Mp_client.execute ~connection:connection ~statement:stmt () in 
    ()
  with
  | Mp_client.Error error ->
      print_newline ();
      print_endline ("This is a test to show how to catch a MySQL error, the exception is: " ^ (Mp_client.error_exception_to_string error))
in
\end{lstlisting}

\section{Disconnect}

To close the connection to the server, use the \texttt{disconnect()} function.

\begin{lstlisting}
let () = Mp_client.disconnect ~connection:connection in
\end{lstlisting}

\end{document}
